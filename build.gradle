import com.matyrobbrt.buildsrc.CreateSrgToOfficial

plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

mixin {
    add sourceSets.main, 'stats.refmap.json'
    config 'stats.mixins.json'
    debug.export = true
}

group = 'com.matyrobbrt.stats'
archivesBaseName = 'stats'


java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: 'parchment', version: '2022.11.27-1.19.2'

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        all {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'stats'
            mods {
                stats {
                    source sourceSets.main

                }
            }
        }

        client {}
        server {}
        gameTestServer {}

        data {
            workingDirectory project.file('run')
            args '--mod', 'stats', '--all',
                    '--output', file('src/generated/resources/'),
                    '--existing', file('src/main/resources/')
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft 'net.minecraftforge:forge:1.19.2-43.2.11'
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    implementation group: 'org.flywaydb', name: 'flyway-core', version: project.flyway_version
    implementation group: 'org.jdbi', name: 'jdbi3-core', version: project.jdbi_version
    implementation group: 'org.jdbi', name: 'jdbi3-sqlobject', version: project.jdbi_version
    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: project.sqlite_version
    implementation 'org.postgresql:postgresql:42.6.0'

    implementation 'com.moandjiezana.toml:toml4j:0.7.2'
    implementation 'io.github.matyrobbrt:curseforgeapi:1.7.4'
    implementation 'net.minecraftforge:srgutils:0.4.3'
    implementation 'net.dv8tion:JDA:5.0.0-beta.9'
    implementation project(':metabase-api')
}

final manifestAttributes = [
        'Specification-Title'   : 'stats',
        'Specification-Vendor'  : 'statssareus',
        'Specification-Version' : '1',
        'Implementation-Title'  : project.name,
        'Implementation-Version': project.jar.archiveVersion,
        'Implementation-Vendor' : 'statssareus'
]
manifestAttributes.put('MixinConfigs', 'stats.mixins.json')

jar {
    manifest.attributes(manifestAttributes)
    finalizedBy('reobfJar')
}

tasks.register('srg2official', CreateSrgToOfficial) {
    minecraftVersion.set('1.19.2')
    outputMappings.set(file('mappings.srg'))
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
